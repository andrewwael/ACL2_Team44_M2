//1. Query 1
MATCH (t:Traveller)-[:FROM_COUNTRY]->(home:Country)
MATCH (t)-[:STAYED_AT]->(:Hotel)-[:LOCATED_IN]->(:City)-[:LOCATED_IN]->(dest:Country)
OPTIONAL MATCH (home)-[v:NEEDS_VISA]->(dest)
WITH t, home, dest, v
WHERE home = dest OR v IS NULL
RETURN count(DISTINCT t) AS travellers_no_visa;

//2. Query 2
MATCH (t:Traveller {type:'Business'})-[:WROTE]->(r:Review)-[:REVIEWED]->(h:Hotel)
WITH h, avg(r.score_overall) AS avg_rating
RETURN h.name AS hotel_name, avg_rating
ORDER BY avg_rating DESC
LIMIT 3;

//3. Query 3
MATCH (h:Hotel)
OPTIONAL MATCH (t:Traveller {type:'Couple'})-[:STAYED_AT]->(h)
WITH h, count(DISTINCT t) AS couple_count
RETURN h.name AS hotel_name, couple_count
ORDER BY hotel_name;

//4. Query 4
MATCH (r:Review)-[:REVIEWED]->(h:Hotel)
WITH h, avg(r.score_cleanliness) AS avg_cleanliness
WHERE avg_cleanliness < 8.8
RETURN h.name AS hotel_name, avg_cleanliness
ORDER BY avg_cleanliness ASC
LIMIT 3;

//5. Query 5
MATCH (t:Traveller {gender:'Female'})-[:WROTE]->(r:Review)-[:REVIEWED]->(h:Hotel)
WITH h, avg(r.score_location) AS avg_loc
WITH collect({hotel:h.name, avg_loc:avg_loc}) AS rows, max(avg_loc) AS max_loc
UNWIND rows AS row
WITH row, max_loc
WHERE row.avg_loc = max_loc
RETURN row.hotel AS hotel_name, row.avg_loc AS avg_location_score
ORDER BY hotel_name;

